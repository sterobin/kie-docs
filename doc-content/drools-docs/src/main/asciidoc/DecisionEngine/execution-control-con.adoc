[id='execution-control-con_{context}']

= Execution control in the {DECISION_ENGINE}

When new rule data enters the working memory of the {DECISION_ENGINE}, rules may become fully matched and eligible for execution. A single working memory action can result in multiple eligible rule executions. When a rule is fully matched, the {DECISION_ENGINE} creates an activation instance, referencing the rule and the matched facts, and adds the activation onto the {DECISION_ENGINE} agenda. The agenda controls the execution order of these rule activations using a conflict resolution strategy.

The {DECISION_ENGINE} cycles repeatedly through two phases to perform actions from rule execution:

* *Working memory actions.* In this phase, the {DECISION_ENGINE} performs the actions of rule consequences (the `then` portion of the rule) or follows the main Java application process. After a consequence action is complete or the main Java application process calls `fireAllRules()`, the {DECISION_ENGINE} moves to the agenda evaluation phase to assess rules.
* *Agenda evaluation.* In this phase, the {DECISION_ENGINE} selects a rule to execute. If no executable rules exist, the execution cycle ends. If an executable rule is found, the {DECISION_ENGINE} executes the rule and returns to the working memory actions phase to perform the actions of the rule consequence or to follow the Java application process.

.Two-phase execution process in the {DECISION_ENGINE}
image::HybridReasoning/Two_Phase.png[align="center"]

This process repeats until the agenda is clear, and then the calling application again has control over the rule execution. When working memory actions are occurring, no rules are executed.

When multiple rules exist on the agenda, the execution of one rule may cause another rule to be removed from the agenda. To avoid this, you can define how and when rules are executed in the {DECISION_ENGINE}. Some common methods for defining rule execution order are by using rule salience, agenda groups, and activation groups for rules.

== Salience for rules

Each rule has an integer `salience` attribute that determines order of execution. Rules with a higher salience value are given higher priority when ordered in the activation queue. The default salience value for rules is zero, but the salience can be negative or positive.

For example, the following sample DRL rules are listed in the {DECISION_ENGINE} stack in the order shown:

[source]
----
rule "RuleA"
salience 95
when
    $fact : MyFact( field1 == true )
then
    System.out.println("Rule2 : " + $fact);
    update($fact);
end

rule "RuleB"
salience 100
when
   $fact : MyFact( field1 == false )
then
   System.out.println("Rule1 : " + $fact);
   $fact.setField1(true);
   update($fact);
end
----

The `RuleB` rule is listed second, but it has a higher salience value than the `RuleA` rule and is there executed first.

== Agenda groups for rules

An agenda group is a set of rules bound together by the same `agenda-group` rule attribute. Agenda groups partition rules on the {DECISION_ENGINE} agenda. At any one time, only one group has a "focus" that gives that group of rules priority for execution before rules in other agenda groups. You determine the focus with a `setFocus()` call for the agenda group. You can also define rules with an `auto-focus` attribute so that the next time the rule is activated, the focus is automatically given to the agenda group to which the rule is assigned.

Each time the `setFocus()` call is made in a Java application, the {DECISION_ENGINE} adds the specified agenda group to the top of the rule stack. The default agenda group `"MAIN"` contains all rules that do not belong to a specified agenda group and is executed first in the stack unless another group has the focus.

For example, the following sample DRL rules belong to the specified agenda groups and are listed in the {DECISION_ENGINE} stack in the order shown:

.Sample DRL rules for banking
[source]
----
rule "increase balance for credits"
  agenda-group "calculation"
when
  ap : AccountPeriod()
  acc : Account( $accountNo : accountNo )
  CashFlow( type == CREDIT,
            accountNo == $accountNo,
            date >= ap.start && <= ap.end,
            $amount : amount )
then
  acc.balance  += $amount;
end
----

[source]
----
rule "Print balance for AccountPeriod"
  agenda-group "report"
when
  ap : AccountPeriod()
  acc : Account()
then
  System.out.println( acc.accountNo +
                      " : " + acc.balance );
end
----

For this example, the rules in the `"report"` agenda group must always be executed first and the rules in the `"calculation"` agenda group must always be executed second. Any remaining rules in other agenda groups can then be executed. Therefore, the `"report"` and `"calculation"` groups receive the focus to be executed in that order, before other rules can be executed:

.Set the focus for the order of agenda group execution
[source,java]
----
Agenda agenda = ksession.getAgenda();
agenda.getAgendaGroup( "report" ).setFocus();
agenda.getAgendaGroup( "calculation" ).setFocus();
ksession.fireAllRules();
----

You can also use the `clear()` method to cancel all the activations generated by the rules belonging to a given agenda group before each has had a chance to be executed:

.Cancel all other rule activations
[source,java]
----
ksession.getAgenda().getAgendaGroup( "Group A" ).clear();
----

== Activation groups for rules

An activation group is a set of rules bound together by the same `activation-group` rule attribute. In this group, only one rule can be executed. After conditions are met for a rule in that group to be executed, all other pending rule executions from that activation group are removed from the agenda.

For example, the following sample DRL rules belong to the specified activation group and are listed in the {DECISION_ENGINE} stack in the order shown:

.Sample DRL rules for banking
[source]
----
rule "Print balance for AccountPeriod1"
  activation-group "report"
when
  ap : AccountPeriod1()
  acc : Account()
then
  System.out.println( acc.accountNo +
                      " : " + acc.balance );
end
----

[source]
----
rule "Print balance for AccountPeriod2"
  activation-group "report"
when
  ap : AccountPeriod2()
  acc : Account()
then
  System.out.println( acc.accountNo +
                      " : " + acc.balance );
end
----

For this example, if the first rule in the `"report"` activation group is executed, the second rule in the group and all other executable rules on the agenda must be removed from the agenda. Therefore, the `"report"` group receives the focus to be executed first and only among other rules on the agenda, if the rule conditions are met:

.Set the focus for activation group execution
[source,java]
----
Agenda agenda = ksession.getAgenda();
agenda.getActivationGroup( "report" ).setFocus();
ksession.fireAllRules();
----
